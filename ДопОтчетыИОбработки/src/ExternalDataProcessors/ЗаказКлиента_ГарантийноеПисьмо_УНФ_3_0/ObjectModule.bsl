#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
    
	ИмяОбработки = ЭтотОбъект.Метаданные().Имя; 
    Синоним = ЭтотОбъект.Метаданные().Синоним; 
    Синоним = ?(ЗначениеЗаполнено(Синоним),Синоним, ИмяОбработки);
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	ПараметрыРегистрации.Вид			 = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиПечатнаяФорма();
	ПараметрыРегистрации.Версия			 = "1.0.0.1";
	ПараметрыРегистрации.Наименование	 = Синоним;
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	ПараметрыРегистрации.Информация		 = "Внешняя печатная форма """ + Синоним + """";
	
	ПараметрыРегистрации.Назначение.Добавить("Документ.ЗаказПокупателя");
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление		 = Синоним;
	Команда.Идентификатор		 = ИмяОбработки;
	Команда.Использование		 = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	Команда.Модификатор	 		 = "ПечатьMXL";
	Команда.ЗаменяемыеКоманды	 = "";// если нужно заменить существующую команду печати, укажите её идентификатор
	Команда.ПоказыватьОповещение = Истина;
    
    Возврат ПараметрыРегистрации; 
    
КонецФункции

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 

	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			ЭтотОбъект.Метаданные().Имя,
			НСтр("ru = 'ГарантийноеПисьмо'"),
			СформироватьПечатнуюФормуГарантийноеПисьмо(МассивОбъектов, ОбъектыПечати),
			,);
	//ФормированиеПечатныхФорм.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);

КонецПроцедуры 

#КонецОбласти

#Область ФункцииФормированияПечатныхФорм

Функция СформироватьПечатнуюФормуГарантийноеПисьмо(МассивОбъектов, ОбъектыПечати) Экспорт

	ТабДок = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("ПФ_MXL_ГарантийноеПисьмо");
	
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказПокупателя.Ссылка КАК Ссылка,
		|	ЗаказПокупателя.Номер КАК Номер,
		|	ЗаказПокупателя.Дата КАК Дата,
		|	ПРЕДСТАВЛЕНИЕ(ЗаказПокупателя.Организация) КАК Организация,
		|	ПРЕДСТАВЛЕНИЕ(ЗаказПокупателя.Контрагент) КАК Контрагент,
		|	ЗаказПокупателя.ДатаОтгрузки КАК СрокОплаты
		|ИЗ
		|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
		|ГДЕ
		|	ЗаказПокупателя.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
			
	//	многостраничная печать
	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
	
		// Вывод таб док
		Область = Макет.ПолучитьОбласть("ТекстПисьма");
		Область.Параметры.Заполнить(Выборка);
		Область.Параметры.Дата = Формат(Выборка.Дата, "ДФ=dd.MM.yyyy");
		Область.Параметры.СрокОплаты = Формат(Выборка.СрокОплаты,"ДФ=dd.MM.yyyy");
		ТабДок.Вывести(Область);
		
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, 
		    НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
		ВставлятьРазделительСтраниц = Истина;

	КонецЦикла;
		
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	//ТабДок.ОриентацияСтраницы =  ОриентацияСтраницы.Ландшафт;

	Возврат ТабДок;
	
КонецФункции	

#КонецОбласти
