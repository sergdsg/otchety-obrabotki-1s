
#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
	
	ИмяОбработки = ЭтотОбъект.Метаданные().Имя; 
    Синоним = ЭтотОбъект.Метаданные().Синоним; 
    Синоним = ?(ЗначениеЗаполнено(Синоним),Синоним, ИмяОбработки);
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	ПараметрыРегистрации.Информация = "Внешняя печатная форма Open Office XML """ + Синоним + """";
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиПечатнаяФорма();
	ПараметрыРегистрации.Версия = "1.0.0.1";
	ПараметрыРегистрации.Назначение.Добавить("Документ.ЗаказПокупателя");
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = Синоним;
	Команда.Идентификатор = ИмяОбработки;
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	Команда.ПоказыватьОповещение = Истина;
	
	Возврат ПараметрыРегистрации;

КонецФункции

#КонецОбласти

#Область Печать

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, ЭтотОбъект.Метаданные().Имя);
	
	ПечатнаяФорма.ОфисныеДокументы	= ОфисныеДокументы(МассивОбъектов);
	ПечатнаяФорма.СинонимМакета		= ЭтотОбъект.Метаданные().Синоним;
	
КонецПроцедуры

Функция ОфисныеДокументы(МассивОбъектов)
	
	ОфисныеДокументы = Новый Соответствие;
	
	Шаблон = "Гарантийное письмо № [Номер] от [Дата] ([Контрагент)]";
	
	РеквизитыДокументов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивОбъектов, "Номер, Дата, Контрагент");
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		РеквизитыДокумента = РеквизитыДокументов[Ссылка];
		РеквизитыДокумента.Дата = Формат(РеквизитыДокумента.Дата, "ДЛФ=D");
		РеквизитыДокумента.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыДокумента.Номер, Истина, Истина);
		
		ИмяДокумента = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, РеквизитыДокумента);
		
		АдресДокумента = СформироватьОфисныйДокумент(Ссылка);

		ОфисныеДокументы.Вставить(АдресДокумента, ИмяДокумента);
	
	КонецЦикла;
	
	Возврат ОфисныеДокументы;
		
КонецФункции

Функция СформироватьОфисныйДокумент(Ссылка)
  
 	// Подготавливаем макет для формирования печатной формы OpenXML
    МакетДокумента = ПолучитьМакет("ПФ_DOC_ГарантийноеПисьмо");
    Макет = УправлениеПечатью.ИнициализироватьМакетОфисногоДокумента(МакетДокумента, Неопределено);
	
	// Создаем структуру областей формируемой печатной формы OpenXМL
	ОписаниеОбластей = Новый Структура;
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ТекстПисьма",	 "Общая");
	
	// Подготавливаем печатную форму в формате офисного документа
	ПечатнаяФорма = УправлениеПечатью.ИнициализироватьПечатнуюФорму(Неопределено, Неопределено, Макет);

	//получаем данные документа
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеквизитыШапки.Номер КАК Номер,
		|	РеквизитыШапки.Дата КАК Дата,
		|	ПРЕДСТАВЛЕНИЕ(РеквизитыШапки.Контрагент) КАК Контрагент,
		|	ПРЕДСТАВЛЕНИЕ(РеквизитыШапки.Организация) КАК Организация,
		|	РеквизитыШапки.ДатаОтгрузки КАК СрокОплаты
		|ИЗ
		|	Документ.ЗаказПокупателя КАК РеквизитыШапки
		|ГДЕ
		|	РеквизитыШапки.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();

	Шапка = РезультатЗапроса.Выгрузить();
		
	ДанныеШапка = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Шапка[0]);
	ДанныеШапка["Номер"] = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеШапка["Номер"], Истина, Истина);
	ДанныеШапка["Дата"] = Формат(ДанныеШапка["Дата"], "ДФ=dd.MM.yyyy");
	ДанныеШапка["СрокОплаты"] = Формат(ДанныеШапка["Дата"], "ДФ=dd.MM.yyyy");
	
	
	// Вывод ТекстПисьма
	Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["ТекстПисьма"]);
	УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеШапка);
		
	// Помещаем сформированную печатную форму в соответствие ОфисныеДокументы
	АдресХранилищаПечатнойФормы = УправлениеПечатью.СформироватьДокумент(ПечатнаяФорма);
	
	//удаление временных файлов
	УправлениеПечатью.ОчиститьСсылки(ПечатнаяФорма);	
	УправлениеПечатью.ОчиститьСсылки(Макет);

	Возврат АдресХранилищаПечатнойФормы;
	
КонецФункции // СформироватьОфисныйДокумент()

#КонецОбласти