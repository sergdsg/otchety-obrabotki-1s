#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
    		
	ИмяОбработки = ЭтотОбъект.Метаданные().Имя; 
    Синоним = ЭтотОбъект.Метаданные().Синоним; 
    Синоним = ?(ЗначениеЗаполнено(Синоним),Синоним, ИмяОбработки);
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.2.2.1");
	ПараметрыРегистрации.Вид			 = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиЗаполнениеОбъекта();
	ПараметрыРегистрации.Версия			 = "1.0.0.1";
	ПараметрыРегистрации.Наименование	 = Синоним;
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	ПараметрыРегистрации.Информация		 = "Обработка заполнения";
	
	ПараметрыРегистрации.Назначение.Добавить("Справочник.Контрагенты");
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление		 = Синоним;
	Команда.Идентификатор		 = ИмяОбработки;
	Команда.Использование		 = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыЗаполнениеФормы();
	Команда.ПоказыватьОповещение = Истина;
    
    Возврат ПараметрыРегистрации; 
	
КонецФункции

Процедура ВыполнитьКоманду(ИмяКоманды, ОбъектыНазначения, ПараметрыВыполнения) Экспорт
	
	ЭтаФорма = ПараметрыВыполнения.ЭтаФорма;
	
	ДанныеКонтрагента = СД_ПолучитьДанныеКонтрагентаПоИНН(ЭтаФорма.Объект.ИНН);
	
	 ЗаполнитьЗначенияСвойств(ЭтаФорма.Объект, ДанныеКонтрагента);
		 
		 НоваяКонтактнаяИнформация = УправлениеКонтактнойИнформацией.НоваяКонтактнаяИнформация();
	
	Если ДанныеКонтрагента.Свойство("ЮридическийАдрес") И ДанныеКонтрагента.ЮридическийАдрес <> Неопределено Тогда
		ДанныеКИ = НоваяКонтактнаяИнформация.Добавить();
		ДанныеКИ.Представление = ДанныеКонтрагента.ЮридическийАдрес;
		ДанныеКИ.Значение = "";
		ДанныеКИ.Вид = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
		
		ДанныеКИ = НоваяКонтактнаяИнформация.Добавить();
		ДанныеКИ.Представление = ДанныеКонтрагента.ЮридическийАдрес;
		ДанныеКИ.Значение = "";
		ДанныеКИ.Вид = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
	КонецЕсли;
    УправлениеКонтактнойИнформацией.УстановитьКонтактнуюИнформациюОбъекта(ЭтаФорма.Объект, НоваяКонтактнаяИнформация);
	ИмяЭлементаДляРазмещения = ЭтаФорма.Элементы.КонтактнаяИнформация.Имя;
	УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтаФорма, ЭтаФорма.Объект, ИмяЭлементаДляРазмещения);
	
	//ЗаполнитьВидКонтрагента();
	//УстановитьЗаголовокЮридическихДанных(ЭтотОбъект);
	
	ЭтаФорма.Модифицированность = Истина;
	
	Если НоваяКонтактнаяИнформация.Количество() = 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'Не удалось получить контактную информацию или контрагент является ""ИП""'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);	
	КонецЕсли;
		
КонецПроцедуры


 Функция СД_ПолучитьДанныеКонтрагентаПоИНН(ИНН)
	 
	 ДанныеКонтрагента = Новый Структура;
	 ДанныеКонтрагента.Вставить("ИНН", ИНН);
	 
	//https://egrul.itsoft.ru/short_data/?3445071523	 
	 АдресСервера = "egrul.itsoft.ru"; 
	
	 АдресРесурса = СтрШаблон("/short_data/?%1",ИНН);
	 
	 ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
	 
	 Соединение = Новый HTTPСоединение(АдресСервера,,,,,30,ЗащищенноеСоединение);
	 Запрос = Новый HTTPЗапрос(АдресРесурса);
	 
	 Попытка
		 Ответ = Соединение.Получить(Запрос);
	 Исключение 
		 ОбщегоНазначения.СообщитьПользователю("Не удалось получить данные по ИНН " + ОписаниеОшибки());
		 Возврат ДанныеКонтрагента;
	 КонецПопытки;
	 
	 СтрокаJSON = Ответ.ПолучитьТелоКакСтроку();

	 ДанныеЮЛ = Неопределено;
	 Если Ответ.КодСостояния = 200 Тогда
		 ЧтениеJSON = Новый ЧтениеJSON;
		 ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
		 
		Попытка
			ДанныеЮЛ = ПрочитатьJSON(ЧтениеJSON,Истина);
			ЧтениеJSON.Закрыть();
	 	Исключение
			ТекстСообщения = "Ошибка получения данных (ИНН не найден!!!)  " + ОписаниеОшибки();
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат ДанныеКонтрагента;
		КонецПопытки;	
	 
	 Иначе
		 ТекстСообщения = СтрШаблон("Получить данные по ИНН не удалось...
		 								|Код состояния: %1
										|Ответ сервера: %2",Ответ.КодСостояния,СтрокаJSON);
		 ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		  Возврат ДанныеКонтрагента;
	  КонецЕсли;
	  
	  Если ДанныеЮЛ["short_form"] <> Неопределено И
		  ДанныеЮЛ["short_form"] = "ИП" Тогда 
		  
		  ВидКонтрагента = Перечисления.ВидыКонтрагентов.ИндивидуальныйПредприниматель;
	  Иначе
		  ВидКонтрагента = Перечисления.ВидыКонтрагентов.ЮридическоеЛицо;  
	  КонецЕсли; 
	  
	    ДанныеКонтрагента.Вставить("ВидКонтрагента", ВидКонтрагента);
	    ДанныеКонтрагента.Вставить("РегистрационныйНомер",ДанныеЮЛ["ogrn"]);
	    ДанныеКонтрагента.Вставить("КПП",ДанныеЮЛ["kpp"]);
	    ДанныеКонтрагента.Вставить("Наименование",ДанныеЮЛ["short_name"]);
		ДанныеКонтрагента.Вставить("НаименованиеПолное", ДанныеЮЛ["full_name"]);
		
		Если ДанныеЮЛ["address"] <> Неопределено Тогда
			ДанныеКонтрагента.Вставить("ЮридическийАдрес", ДанныеЮЛ["address"]);
		КонецЕсли;
		
		Возврат ДанныеКонтрагента;
		
 КонецФункции


#КонецОбласти
