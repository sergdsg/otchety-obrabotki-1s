
#Область ОбработчикиСобытий


Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)

	 СтандартнаяОбработка = Ложь;
	 
	 КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	 Макет = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,КомпоновщикНастроек.ПолучитьНастройки(),ДанныеРасшифровки);
	
	 ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	 ПроцессорКомпоновки.Инициализировать(Макет,,ДанныеРасшифровки,Истина);
	 
	 ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	 ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	 ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	 
	 // Корректировка ДокументРезультат
	 КоличествоКолонок = ДокументРезультат.ШиринаТаблицы;
	 КоличествоСтрок =  ДокументРезультат.ВысотаТаблицы;
	 
	 // Размер изображений
	 ВысотаСтроки = 60;
	 ШиринаКолонки = 20;
	 
	 Для НомерКолонки = 1 По КоличествоКолонок Цикл
		 Для НомерСтроки = 1 По КоличествоСтрок Цикл 
			 
			 ТекущаяЯчейка = ДокументРезультат.Область(НомерСтроки, НомерКолонки);
			 
			 Если ТекущаяЯчейка.Расшифровка <> Неопределено Тогда
				  ПоляРасшифровки = ДанныеРасшифровки.Элементы[ТекущаяЯчейка.Расшифровка].ПолучитьПоля();
				  
				  Если ПоляРасшифровки.Количество() > 0 Тогда 
					  
					  ТекущееПолеРасшифровки =  ПоляРасшифровки[0].Значение;
					  
					  Если ТипЗнч(ТекущееПолеРасшифровки) = 
						   						Тип("СправочникСсылка.НоменклатураПрисоединенныеФайлы") 
						  						 И ЗначениеЗаполнено(ТекущееПолеРасшифровки) Тогда
											
							ДвоичныеДанныефайлаКартинки = РаботаСФайлами.ДвоичныеДанныеФайла(ТекущееПолеРасшифровки);	
							Если ДвоичныеДанныефайлаКартинки <> Неопределено Тогда 
								
					              ТекущаяЯчейка.ВысотаСтроки = ВысотаСтроки;
								  ТекущаяЯчейка.ШиринаКолонки = ШиринаКолонки;
								  
								  Рисунок = ДокументРезультат.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
								  Рисунок.РазмерКартинки = РазмерКартинки.Пропорционально;
								  Рисунок.Картинка = Новый Картинка(ДвоичныеДанныефайлаКартинки);
								  Рисунок.Расположить(ТекущаяЯчейка);
								  Рисунок.ГраницаСверху = Ложь;
								  Рисунок.ГраницаСнизу = Ложь;
								  Рисунок.ГраницаСлева = Ложь;
								  Рисунок.ГраницаСправа = Ложь;		  
								  
					        КонецЕсли
					КонецЕсли;  
				КонецЕсли;	  
			КонецЕсли;	 
		КонецЦикла;		 
	КонецЦикла;			 
				 
КонецПроцедуры

Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("3.1.1.1");
	ПараметрыРегистрации.Информация = НСтр("ru = 'Цены номенклатуры с картинками'");
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительныйОтчет();
	ПараметрыРегистрации.Версия = "1.0.0.0";
//	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = НСтр("ru = 'Прайс лист с картинками'");
	Команда.Идентификатор = "ОткрытиеФормы";
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	Команда.ПоказыватьОповещение = Истина;
	

 	Возврат ПараметрыРегистрации;
 	
КонецФункции
#КонецОбласти